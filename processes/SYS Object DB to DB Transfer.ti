#region Prolog
cubename = 'SYS Change Log';
viewname = 'Flag';

if(ViewExists( cubename, viewname )=0);
    ViewCreate( cubename, viewname, 0 );
endif;

ViewExtractSkipZeroesSet( cubename, viewname, 1 );

dimname = 'm_Last Modified';

If(SubsetExists( dimname, viewname )=1);
    SubsetDestroy( dimname, viewname );
endif;
SubsetCreate( dimname, viewname, 0 );

SubsetElementInsert( dimname, viewname, 'Flag for Promotion', 1 );
ViewSubsetAssign( cubename, viewname, dimname, viewname );

dimname = 'Change Log Field';

If(SubsetExists( dimname, viewname )=1);
    SubsetDestroy( dimname, viewname );
endif;
SubsetCreate( dimname, viewname, 0 );

SubsetElementInsert( dimname, viewname, 'File Last Modified', 1 );
ViewSubsetAssign( cubename, viewname, dimname, viewname );

DatasourceCubeview = viewname;

# full paths to data folders
source=CellGetS( 'SYS Model Settings', 'Data Folder Path', 'Input' );

dest=CellGetS( 'SYS Model Settings', 'Test Database Path', 'Input' );

## Check if caption attribute exists and if not, add it (because github doesnt work properly with control objects)
dimname = 'Change Log Field';
AttrName = 'Caption';
IF ( DIMIX ('}Element_Attributes_' | DimName, AttrName ) = 0);
    ATTRINSERT ( DimName,'', AttrName ,'a');
ENDIF;
#endregion
#region Data
dimname = 'Change Log Field';
if(CellGetS( cubename, 'File Last Modified', vLine, 'Flag for Promotion' )@='Y');
    file=CellGetS( 'SYS Change Log', 'File Last Modified', vLine , 'Object');
    
    command = 'xcopy "' | source | '\' | file | '" "' | dest | '" /Y';
    ExecuteCommand( command, 1 );

    sTimestamp = TIMST( now(), '\Y\m\d\h\i' );
    sPrettyTimestamp = timst(now(), '\d/\m/\Y \h:\i ');
    
    
    DimensionElementInsertDirect( dimname, '', sTimestamp, 'n' );
    AttrPutS( sPrettyTimestamp, dimname, sTimestamp, AttrName);
    
    sComment = CellGetS( cubename, 'File Last Modified', vLine, 'Comment' );
    sUser = attrs( '}Clients', TM1User(), '}TM1_DefaultDisplayValue' );
    CellPutS( sComment | ' User: ' | subst(sUser, SCAN( '/', sUser)+1, long(sUser)), cubename, sTimestamp, vLine, 'Comment' );
    
    sObject = CellGetS( cubename, 'File Last Modified', vLine, 'Object' ); 
    CellPutS( sObject, cubename,  sTimestamp, vLine, 'Object' );
    
    sModified = CellGetS( cubename, 'File Last Modified', vLine, 'Last Modified' ); 
    CellPutS( sModified, cubename,  sTimestamp, vLine, 'Last Modified' );

    CellPutS( 'Promoted ' | sPrettyTimestamp, cubename, 'File Last Modified', vLine, 'Comment' );
endif;
#endregion