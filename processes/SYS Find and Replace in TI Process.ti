#region Prolog
#Region ####### PUT IN PROLOGUE OF PROCESS TO LOG: #######
pPrologNow = Now; 
# Catch errors with if statement and do nErrors = nErrors+1;
nErrors = 0;   
# Redefine message depending on if statement that handled the exception to help user
pMessage = 'Process completed successfully.';    
sMessage1 = '';
sMessage2 = '';
sMessage3 = '';
# Concatenate the parameters together to write into output cube. pParam1 | ', ' | pParam2 | ', ' | pParam3 | ....
pParameters ='pSourceName: ' | pSourceName | ', pSourceInstance: ' | pSourceInstance | ', pSourceDB: ' | pSourceDB | ', pFind: ' | ', ' | pFind | ', pReplace: ' | pReplace;
#EndRegion


# Check pSourceName for spaces and replace with %20
i=1;
while (i>0);
    i = SCAN(' ',pSourceName);
    if(i<>0);
        pSourceName = DELET(pSourceName,i,1);
        pSourceName = INSRT('%20',pSourceName,i);
    endif;
end;

#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Define environment variables
#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Global settings for the client
host = CellGetS( 'SYS PA API Credentials', 'Input', 'host' );
apiKey =  CellGetS( 'SYS PA API Credentials', 'Input', 'apiKey' );

# Convert apiKey to base 64 encoding
apiKey = Base64Encode( 'apikey:'| apiKey);

# Tenant ID's for the instances that exist
DevTenant =CellGetS( 'SYS PA API Credentials', 'Input', 'DevTenant' );
ProdTenant = CellGetS( 'SYS PA API Credentials', 'Input', 'ProdTenant' );

# Select which tenant to use for the source
If(pSourceInstance @= 'Dev');
    SourceTenant = DevTenant;
elseif( pSourceInstance @= 'Prod' );
    SourceTenant = ProdTenant;
endif;

DestinationTenant = SourceTenant;
# Set databases to copy from and to
SourceDB = pSourceDB;
DestinationDB = pSourceDB;
pDestDB = pSourceDB;

# Define the root URL for the source and destination for the API calls
SourceRoot = host |'/api/' |SourceTenant | '/v0/tm1/' |SourceDB|'/api/v1/'; 
DestinationRoot = SourceRoot;


#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# GET DATA FROM SOURCE DATABASE
#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Request a session cookie that means further requests don't need authentication token
SourceHeader = '-H Authorization: Basic ' | apiKey;
ExecuteHttpRequest('GET', host | '/api/' | SourceTenant | '/v0/rolemgmt/v1/users/me', SourceHeader);

# Check the response and end the process if authentication failed
ResponseBody = HttpResponseGetBody();
ResponseStatus = HttpResponseGetStatusCode();
If(ResponseStatus <> 200);
    ItemReject('Error authenticating to source environment: ' | ResponseBody);
EndIf;

# Get Process data from source database
url = SourceRoot |'Processes('''|pSourceName|''')';
ExecuteHttpRequest('GET', url, SourceHeader);

# Check the response and end the process if authentication failed
ResponseProcess = HttpResponseGetBody();
ResponseStatus = HttpResponseGetStatusCode();
If(ResponseStatus > 299);
       ItemReject('Error retrieving process '|pSourceName|' in  '|pSourceDB|'  Error code: '|NumberToString( ResponseStatus)|' Response: ' | ResponseBody);
EndIf;



#Region #~~~~~~~~~~Actual find and replace part ~~~~~~~~~~~#
#find and replace in prolog
prolog = JsonGet( ResponseProcess, 'PrologProcedure' );
FindLong = LONG( pFind );
FindScan = SCAN( pFind, prolog);
while(FindScan<>0);
    prolog = SUBST( prolog, 1, FindScan-1) | pReplace | SUBST( prolog, FindScan+FindLong, long(prolog));
    FindScan = SCAN( pFind, prolog );
end;
ResponseProcess = JsonReplace( ResponseProcess, 'PrologProcedure', prolog );


#find and replace in metadata
metadata = JsonGet( ResponseProcess, 'MetadataProcedure' );
FindLong = LONG( pFind );
FindScan = SCAN( pFind, metadata );
while(FindScan<>0);
    metadata = SUBST( metadata, 1, FindScan-1) | pReplace | SUBST( metadata, FindScan+FindLong, long(metadata));
    FindScan = SCAN( pFind, metadata);
end;
ResponseProcess = JsonReplace( ResponseProcess, 'MetadataProcedure', metadata );

ItemReject( metadata );

#find and replace in data
data = JsonGet( ResponseProcess, 'DataProcedure' );
FindLong = LONG( pFind );
FindScan = SCAN( pFind, data );
while(FindScan<>0);
    data = SUBST( data, 1, FindScan-1) | pReplace | SUBST( data, FindScan+FindLong, long(data));
    FindScan = SCAN( pFind, data );
end;
ResponseProcess = JsonReplace( ResponseProcess, 'DataProcedure', data );

#find and replace in epilog
epilog = JsonGet( ResponseProcess, 'EpilogProcedure' );
FindLong = LONG( pFind );
FindScan = SCAN( pFind, epilog );
while(FindScan<>0);
    epilog = SUBST( epilog, 1, FindScan-1) | pReplace | SUBST( epilog, FindScan+FindLong, long(epilog));
    FindScan = SCAN( pFind, epilog );
end;
ResponseProcess = JsonReplace( ResponseProcess, 'EpilogProcedure', epilog);
#EndRegion

#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# UPDATE THE DESTINATION DATABASE
#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Request a session cookie that means further requests don't need authentication token 
# This isn't needed if the source and target databases are on the same instance, but when copy from one instance to another it is, so just do it anyway.
DestinationAuthHeader = '-H Authorization: Basic ' | apiKey;
ExecuteHttpRequest('GET', host | '/api/' | DestinationTenant | '/v0/rolemgmt/v1/users/me', DestinationAuthHeader);

# Check the response and end the process if authentication failed
ResponseBody = HttpResponseGetBody();
ResponseStatus = HttpResponseGetStatusCode();
If(ResponseStatus <> 200);
    ItemReject('Error authenticating to destination environment: ' | ResponseBody);
EndIf;

# Define header
DestinationHeader = '-H Content-Type: application/json; charset=utf-8';

# Define endpoint
pDestinationName = pSourceName;
url = DestinationRoot |'Processes('''|pDestinationName|''')';

# Prepare data
DestinationData = ResponseProcess;

# Check if process already exists on target
url = DestinationRoot |'Processes('''|pDestinationName|''')';
ExecuteHttpRequest('GET', url, DestinationAuthHeader);
ResponseStatus = HttpResponseGetStatusCode();
ResponseBody = HttpResponseGetBody();

If(ResponseStatus < 300);
    Exists = 1;
    else;
    Exists = 0;
endif;

#ItemReject( url | ' ' | NumberToString( ResponseStatus ) | ' ' | NumberToString( Exists ) | ' ' | ResponseBody);

# If the process Doesn't already exist, create it
If (Exists = 0);
    url = DestinationRoot |'Processes';
    ExecuteHttpRequest('POST', url, DestinationAuthHeader, DestinationHeader,  '-d ' | DestinationData );
    
    ResponseBody = HttpResponseGetBody();
    ResponseStatus = HttpResponseGetStatusCode();
    If(ResponseStatus <> 201);
           ItemReject('Error updating process '|pDestinationName|' in  '|pDestDB|'  Error code: '|NumberToString( ResponseStatus)|' Response: ' | ResponseBody);
    EndIf;
Endif;

# If the process already exists, update it (can't update attributes via PATCH)
If (Exists = 1);
    DestinationData = JsonRemove( DestinationData, 'Attributes' );
    
    url = DestinationRoot |'Processes('''|pDestinationName|''')';
    ExecuteHttpRequest('PATCH', url, DestinationAuthHeader, DestinationHeader,  '-d ' | DestinationData );
    
    ResponseBody = HttpResponseGetBody();
    ResponseStatus = HttpResponseGetStatusCode();
    If(ResponseStatus <> 200);
           ItemReject('Error updating process '|pDestinationName|' in  '|pDestDB|'  Error code: '|NumberToString( ResponseStatus)|' Response: ' | ResponseBody);
    EndIf;
Endif;


#endregion
#region Data


#endregion
#region Epilog

#Region ####### PUT IN EPILOGUE OF PROCESS TO LOG: ##############
# Put messages here to explain exceptions to user
if(nErrors>0);
    pMessage = sMessage1 |sMessage2 | sMessage3 ;   
endif;
RunProcess('SYS Process Logging', 'pProcessName',GetProcessName(), 'pUser', tm1user, 'pPrologNow', pPrologNow, 'pErrors', nErrors, 'pMessage', pMessage, 'pParameters', pParameters);
####### pMessage and nErrors must be defined in the process to log with exeption handling.
#EndRegion


#endregion