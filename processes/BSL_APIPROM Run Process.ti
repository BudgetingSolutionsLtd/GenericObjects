#region Prolog

#Region PROCESS LOGGING
pPrologNow = Now; 
# Catch errors with if statement and do nErrors = nErrors+1;
nErrors = 0;   
# Redefine message depending on if statement that handled the exception to help user
pMessage = 'Process completed successfully';    
sMessage1 = '';
sMessage2 = '';
sMessage3 = '';
# Concatenate the parameters together to write into output cube. pParam1 | ', ' | pParam2 | ', ' | pParam3 | ....
pParameters ='pProcessName: ' | pProcessName | ', pSourceInstance: ' | pSourceInstance | ', pSourceDB: ' | pSourceDB; 
#EndRegion



#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Define environment variables
#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Global settings for the client
host = CellGetS( 'BSL_APIPROM Credentials', 'Input', 'host' );
apiKey =  CellGetS( 'BSL_APIPROM Credentials', 'Input', 'apiKey' );

# Convert apiKey to base 64 encoding
apiKey = Base64Encode( 'apikey:'| apiKey);

# Tenant ID's for the instances that exist
DevTenant =CellGetS( 'BSL_APIPROM Credentials', 'Input', 'DevTenant' );
ProdTenant = CellGetS( 'BSL_APIPROM Credentials', 'Input', 'ProdTenant' );

# Select which tenant to use for the source
If(pSourceInstance @= 'Dev');
    SourceTenant = DevTenant;
elseif( pSourceInstance @= 'Prod' );
    SourceTenant = ProdTenant;
endif;



# Set databases to copy from and to
SourceDB = pSourceDB;


# Define the root URL for the source and destination for the API calls
SourceRoot = host |'/api/' |SourceTenant | '/v0/tm1/' |SourceDB|'/api/v1/'; 


#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# RUN PROCESS
#-------------------------------------------------------------------------------------------------------------------------------------------------------------
# Request a session cookie that means further requests don't need authentication token
SourceHeader = '-h Authorization: Basic ' | apiKey;
ExecuteHttpRequest('GET', host | '/api/' | SourceTenant | '/v0/rolemgmt/v1/users/me', SourceHeader);

# Check the response and end the process if authentication failed
responseBody = HttpResponseGetBody();
status = HttpResponseGetStatusCode();
If(status > 299);
    ItemReject('Error authenticating to source environment: ' | responseBody);
EndIf;

# Define header
DataHeader = '-H Content-Type: application/json; charset=utf-8';

# Run process
url = SourceRoot  | '/Processes('''|pProcessName|''')/tm1.ExecuteWithReturn?$expand=ErrorLogFile';

ExecuteHttpRequest('POST', url, DataHeader);
Response = HttpResponseGetBody();

# Check the response and end the process if authentication failed
ResponseBody = HttpResponseGetBody();
ResponseStatus = HttpResponseGetStatusCode();
If(ResponseStatus > 299);
       ItemReject('Error receiving process '|pProcessName|' in  '|pSourceDB|'  Error code: '|NumberToString( ResponseStatus)|' Response: ' | ResponseBody);
EndIf;



#endregion
#region Data


#endregion
#region Epilog

#Region PROCESS LOGGING
# Put messages here to explain exceptions to user
if(nErrors>0);
    pMessage = sMessage1 |sMessage2 | sMessage3 ;   
endif;
ExecuteProcess('SYS Process Logging', 'pProcessName',GetProcessName(), 'pUser', tm1user, 'pPrologNow', pPrologNow, 'pErrors', nErrors, 'pMessage', pMessage, 'pParameters', pParameters);
####### pMessage and nErrors must be defined in the process to log with exeption handling.
#EndRegion

#endregion