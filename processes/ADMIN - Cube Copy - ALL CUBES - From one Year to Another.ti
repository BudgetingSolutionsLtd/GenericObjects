#region Prolog


# Check Period from exists
IF(Dimix('Period', pYearFrom)=0);
  ProcessError; 
Endif;

# Check Period To exists
IF(Dimix('Period', pYearTo)=0);
  ProcessError; 
Endif;


# Create a list of all cubes to loop through
sDim = '}Cubes';
sSubset = GetProcessName;
nTemp = 1;

SubsetDestroy( sDim, sSubset );
SubsetCreate( sDim, sSubset, nTemp );
SubsetMDXSet( sDim, sSubset, 'TM1Filterbylevel(TM1SubsetAll(['| sDim |']),0)' );
SubsetMDXSet( sDim, sSubset, '' );

DatasourceDimensionSubset = sSubset;
DatasourceNameForClient = sDim;
DatasourceNameForServer = sDim;
DataSourceType = 'SUBSET';


#endregion
#region Data

sCube = vCube;

nDim=CubeDimensionCountGet( sCube);
iDim = 1;
While (iDim <= nDim);
    sDim = TABDIM( sCube, iDim ); 
    IF(sDim @='Period');
        ExecuteProcess( 'EXPORT - Any Cube to CSV', 'pCube', sCube, 'pYear', pYearFrom, 'pVersion', pVersion);

        sFileLocation = GetProcessErrorFileDirectory;
        sFileLocation = Subst(sFileLocation, 1, LONG( sFileLocation )-5)|'Data\Model_Upload\';
        sFileName = sCube | ' Extract.csv';

        IF(FileExists( sFileLocation |sFileName)<>0);
            ExecuteProcess( 'IMPORT - Any Cube from CSV', 'pCube', sCube, 'pYear', pYearTo, 'pVersion', pVersion);
            ExecuteProcess( 'ADMIN - ASCII Delete', 'pLocation', sFileLocation, 'pFile', sFileName);
        Endif;
      Endif;
      iDim = iDim+1;
End;

#endregion
#region Epilog

ExecuteProcess( 'ADMIN - Save Data All' );
#endregion