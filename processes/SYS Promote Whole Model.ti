#region Prolog
if(p1@<>'Y' % p2@<>'Y');
    ProcessQuit;
endif;

cubename = 'SYS Change Log';
viewname = 'Object';

if(ViewExists( cubename, viewname )=1);
    ViewDestroy( cubename, viewname );
endif;
    ViewCreate( cubename, viewname, 0 );

ViewExtractSkipZeroesSet( cubename, viewname, 1 );

dimname = 'm_Last Modified';

If(SubsetExists( dimname, viewname )=1);
    SubsetDestroy( dimname, viewname );
endif;
SubsetCreate( dimname, viewname,  0 );

SubsetElementInsert( dimname, viewname, 'Object', 1 );
ViewSubsetAssign( cubename, viewname, dimname, viewname );

dimname = 'Change Log Field';

If(SubsetExists( dimname, viewname )=1);
    SubsetDestroy( dimname, viewname );
endif;
SubsetCreate( dimname, viewname, 0 );

SubsetElementInsert( dimname, viewname, 'File Last Modified', 1 );
ViewSubsetAssign( cubename, viewname, dimname, viewname );

DatasourceCubeview = viewname;

## Check if caption attribute exists and if not, add it (because github doesnt work properly with control objects)
dimname = 'Change Log Field';
AttrName = 'Caption';
IF ( DIMIX ('}Element_Attributes_' | DimName, AttrName ) = 0);
    ATTRINSERT ( DimName,'', AttrName ,'a');
ENDIF;

# full paths to data folders
source=CellGetS( 'SYS Model Settings', 'Data Folder Path', 'Input' );

dest=CellGetS( 'SYS Model Settings', 'Test Database Path', 'Input' );

ExecuteCommand( '.\model_upload\SYS Scripts\SYS_DB_Tfer_Local.bat' | ' ' | source | ' ' | dest, 1 );   

sTimestamp = TIMST( now(), '\Y\m\d\h\i' );
sPrettyTimestamp = timst(now(), '\d/\m/\Y \h:\i ');
#endregion
#region Data

DimensionElementInsertDirect( dimname, '', sTimestamp | ' Whole Model to "Test"', 'n' );
AttrPutS( sPrettyTimestamp| ' Whole Model to "Test"', dimname, sTimestamp | ' Whole Model to "Test"', AttrName);
    
sComment = CellGetS( cubename, 'File Last Modified', vLine, 'Comment' );
sUser = attrs( '}Clients', TM1User(), '}TM1_DefaultDisplayValue' );
CellPutS( sComment | ' User: ' | subst(sUser, SCAN( '/', sUser)+1, long(sUser)), cubename, sTimestamp | ' Whole Model to "Test"', vLine, 'Comment' );
    
sObject = CellGetS( cubename, 'File Last Modified', vLine, 'Object' ); 
CellPutS( sObject, cubename,  sTimestamp | ' Whole Model to "Test"', vLine, 'Object' );
    
sModified = CellGetS( cubename, 'File Last Modified', vLine, 'Last Modified' ); 
CellPutS( sModified, cubename,  sTimestamp | ' Whole Model to "Test"', vLine, 'Last Modified' );

CellPutS( 'Promoted ' | sPrettyTimestamp, cubename, 'File Last Modified', vLine, 'Comment' );
#endregion
#region Epilog

#endregion